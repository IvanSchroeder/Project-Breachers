@tool
class_name GaeaGenerator3D
extends GaeaGenerator

## Used to transform a world position into a map position,
## mainly used by the [ChunkLoader]. May also be used by
## a [GaeaRenderer]. Otherwise doesn't affect generation.[br]
## [b]In meters.
@export var tile_size: Vector3 = Vector3(1, 1, 1)
## Sets the generator to be used for the next pass. This generates a new grid on top of the one
## generated by this one.[br][br]
## If you're using a [GaeaRenderer], set its [param generator] to the last generator in the chain.[br]
## If you're using a [ChunkLoader3D], set its [param generator] to the first one.[br][br]
## [b]Note:[/b] Using modifiers instead of multiple generators is recommended. Only chain generators if necessary.
@export var next_pass: GaeaGenerator3D


func _ready() -> void:
	grid = GaeaGrid3D.new()
	super()


## Returns the [GaeaGrid3D] resource handling this generator's grid.
func get_grid() -> GaeaGrid3D:
	if not is_instance_valid(grid):
		grid = GaeaGrid3D.new()
	return grid


## Returns the map coordinates of the cell containing the given [param global_position].
func global_to_map(global_position: Vector3) -> Vector3i:
	return (global_position / tile_size).floor()


## Returns the global position of the cell at the given [param map_position].
func map_to_global(map_position: Vector3i) -> Vector3:
	return Vector3(map_position) * tile_size
